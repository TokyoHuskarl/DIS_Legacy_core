def Const_AgentMetaTeam_beginMINUSONE = Const_AgentMetaTeam_begin -1 
__fn debug_selecting_agt $agtidptr $block {
	__if DIS_DEBUG_BUILD == 1 {
		@if v[$agtidptr] == v[101] {
			$block
		}
	}
}

v[4599] = 0
v[535] += 1
v[533] = (v[204] / 52 + 1) * v[1134]
val_mod(v[535],v[533]) // v[535] %= v[533]
v[536] = v[4532]

v[4031] = v[1146]
v[4032..4033] = v[1145]
v[4034] = v[206]
v[4035..4036] = v[205]
//Check ground type troops agents all

// set expanding temp array for copying
// v[4100..4112] = 100000000
v[4100].repeat 100000000, 13
// WITHOUT AEAB, above part is necessarily


@while v[v[536]] != 0 {
		v[212] = v[v[536]] * 300 
		val_add(v[212],N4700) // v[212] += 4700
		v[v[212]].copy v[600], 301

		//Check agent timer to run search AI

		// !!!!!!!!!!!!!!!!!!!!!!!!!!!!!
		// NEVER SAVE BACK agent_generated_ID in this proc!
		// !!!!!!!!!!!!!!!!!!!!!!!!!!!!!

		// @if `(v[900] + v[535]) % v[533] == 0 { //9.5.23
		val_add(agent_generated_ID,v[535])
		val_mod(agent_generated_ID,v[533])
		@if agent_generated_ID == 0 {
				v[211] = v[v[536]] - 1
				AgentIDholder = v[v[536]] 
				v[v[212] + 3] &= -134217729//~134217728

				//If worker AI flag is on
				@if `agent_AgentBits & AgentBits_FLAG_EnableCivAI {

						
						@if `agent_AgentBits & AgentBits_FLAG_walldetected {
								@if `!(agent_AI_routine_bits & (AI_routine_bits_FLAG_tracing_flag + agent_AI_routine_bits & AI_routine_bits_FLAG_WP_set_by_RayCast)){
										TT1 = v[212] + 3
										v[TT1] |= AgentBits_FLAG_PF_walldetection_switch
										TT1 += 31
										v[TT1] |= AI_routine_bits_FLAG_1secDelay_PF
										
								}
								
						}
						
						
				} .else bl {
						@if `agent_AgentBits & AgentBits_FLAG_EnableBasicAI { //9.5.23
								agent_AI_routine_bits = v[212] + 34
								@if `v[agent_AI_routine_bits] & AI_routine_bits_FLAG_AIskip {
										v[agent_AI_routine_bits] &= -2097153 //~2097152
										
								} .else bl {
									Ptr20 = v[212] + 18
									Ptr19 = v[212] + 241
									//get team ID
									// Temp1 = v[212] + 100

									//team ID % 3
									__if DIS_EXPERIMENTAL == 1 {
										//macro_get_agent_MetaTeam_into_var(v[v[536]],Temp14)
														get_AEB_element_wo_ptr(AgentIDholder,__id(Temp14),AgentExBuffer_SLOT_MetaTeam)
									}.else bl {
										Temp14 = Const_AgentMetaTeam_begin + v[211]
										Temp14.deref Temp14,1 // Temp14 = v[Temp14]
									}

									// Temp8 = v[4034 + Temp14]
									Temp8 = 4034 + Temp14
									Temp8.deref Temp8,1

									__if DIS_EXPERIMENTAL == -1 { // use AEBA to opt! also @if cond can be changed into other shit.
											get_AEB_array(AgentIDholder,4001,AgentExBuffer_SLOT_Class_Target_Priority_Array,ClassType_Amount)

									}.else bl {

									agent_AI_routine_bits.deref Temp7,1 // Temp7 = v[agent_AI_routine_bits]
									v[4101].copy v[4001],12

								// Common_Switch5 serves checking if the agent has bias to prioritize the enemy by its type, if it's on, it means the agent has the bias
									LEGS_Common_Switch5.off
									@if `Temp7 & 1 {
											v[4001..4012] += 900000000
											v[4012] -= 950000000 // wut
											LEGS_Common_Switch5.on
											
									}
									@if `Temp7 & 2 {
											v[4002] -= 50000000
											LEGS_Common_Switch5.on
											
									}
									@if `Temp7 & 4 {
											v[4005] -= 50000000
											LEGS_Common_Switch5.on
											
									}
							}

										Temp7 = 0
										// set ptr to array for comparation of agents
										v[534] = v[4579] + (v[1012] + 2) * v[211]
										v[v[534]] = 0

										agent_UnitEyeSight_for_AgentSearch.copy Temp8,1

										// get sight range root
										defv UnitAI_agent_eyesight = 315
										__if DIS_EXPERIMENTAL == -1 { // use AEBA to opt! also @if cond can be changed into other shit.

											get_AEB_element_wo_ptr(AgentIDholder,__id(UnitAI_agent_eyesight),AgentExBuffer_SLOT_BaseSight_Root)

										}.else bl {

											UnitAI_agent_eyesight = v[211] + v[4806]
											UnitAI_agent_eyesight.deref UnitAI_agent_eyesight,1
										}

										//If the agent has target agent
										//Temp16 = Temp15  
										@if v[Ptr20] >= 1 {
												//then halve search radius
												UnitAI_agent_eyesight >>= 1 // UnitAI_agent_eyesight /= 2
											//If it's in melee combat, 
												@if agent_MeleeFightTimer >= 1 {
														//AND melee unit
														UnitAI_agent_eyesight >>= agent_AAType == 0 ? 1 : 0

												}
												
										}


										UnitAI_agent_eyesight.copy v[316],1 // UnitAI_agent_eyesight is v[315]



										// DEBUG
										debug_selecting_agt(v[536],{
											agent_RelativeX.copy TT1, 2
											TT1 .sub UnitAI_agent_eyesight,2 // v[341..342] -= UnitAI_agent_eyesight
											TT1 .add v[53], 2
											agent_RelativeX.copy TT3,2
											TT3 .add UnitAI_agent_eyesight,2 // v[341..342] -= UnitAI_agent_eyesight
											TT3 .add v[53], 2
											TT3 -= TT1
											TT4 -= TT2
											@pic[10].strpic {
												"CHECK"
												.pos TT1, TT2 .topLeft
												.size TT3, TT4                        .chromakey 1
												.scale 100
												.trans 80
												.rgbs 100, 100, 100, 100
												.font Font_Common, Font_Common_size
												.spacing 0, 4
												.skin ""  .noPadding
												.mapLayer 8
												.eraseWhenTransfer
												.affectedByFlash
												.affectedByShake
										}

										})

										/*@comment "#########################		    #2002 Crd->Mort V1V2 reg1		    ##########################"*/
										agent_RelativeX.copy TT1, 2
										TT1 .sub UnitAI_agent_eyesight,2 // v[341..342] -= UnitAI_agent_eyesight
										TT1 .add v[53], 2
										TT1 .sub v[76], 2
										TT1 .div v[74], 2
										TT1 &= 31 // TT1 = max(TT1, 31)
										TT2 &= 31 // TT2 = max(TT2, 31)

										/*@comment "TT1=MortX
			 TT2=MortY
			 ビット演算して処理するよ"*/
										TT1 = (TT1 | TT1 << 8) & 0xFF00FF
										TT1 = (TT1 | TT1 << 4) & 0xF0F0F0F
										TT1 = (TT1 | TT1 << 2) & 0x33333333
										TT1 = (TT1 | TT1 << 1) & 0x55555555
										TT2 = (TT2 | TT2 << 8) & 0xFF00FF
										TT2 = (TT2 | TT2 << 4) & 0xF0F0F0F
										TT2 = (TT2 | TT2 << 2) & 0x33333333
										TT2 = ((TT2 | TT2 << 1) & 0x55555555)<<1

										//@comment "yは1bitシフトで終わり"
										//TT2 <<= 1
										//@comment "最後にTT1 OR TT2"
										//opt 28.4.23
										//v[21] = TT1 | TT2
										/*@comment "#########################		   #2002 Crd->Mort V1V2 reg1 end		   ##########################"*/
										v[351] = TT1 | TT2

										/*@comment "##########################2002 Crd->Mort V1V2 reg1##########################"*/
										agent_RelativeX.copy TT1, 2
										TT1 .add UnitAI_agent_eyesight,2 // v[341..342] += UnitAI_agent_eyesight
										TT1 .add v[53], 2
										TT1 .sub v[76], 2
										TT1 .div v[74], 2
										TT1 &= 31 // TT1 = min(TT1, 31)
										TT2 &= 31 // TT2 = min(TT2, 31)

										/*@comment "TT1=MortX
									TT2=MortY
									ビット演算して処理するよ"*/
										TT1 = (TT1 | TT1 << 8) & 0xFF00FF
										TT1 = (TT1 | TT1 << 4) & 0xF0F0F0F
										TT1 = (TT1 | TT1 << 2) & 0x33333333
										TT1 = (TT1 | TT1 << 1) & 0x55555555
										TT2 = (TT2 | TT2 << 8) & 0xFF00FF
										TT2 = (TT2 | TT2 << 4) & 0xF0F0F0F
										TT2 = (TT2 | TT2 << 2) & 0x33333333
										TT2 = ((TT2 | TT2 << 1) & 0x55555555) << 1
										//@comment "yは1bitシフトで終わり"
										//TT2 <<= 1
										//@comment "最後にTT1 OR TT2"

				//opt 28.4.23
										//v[21] = TT1 | TT2
										TT2 = TT1 | TT2
										val_asg(TT1,v[351]) // TT1 = v[351]
										//TT2 = v[21]
										/*
										@comment "TT1 = upper left morton
										TT2 = bottom right morton
										TT5 = shift num"
										*/

										TT3 = TT1 ^ TT2

										//If the morton space where you're gonna check is the root space...
										@if `TT3 & 768 {

											Temp2 = 4031 + Temp14
											Temp2.deref Temp2,1 // Temp2 = v[Temp2]

											//then just check team agent list - but this seemingly not good design (also kinda buggy), so maybe I need to change this later
											Temp6 = 3601

											//@if `v[agent_AI_routine_bits] & 7 { // EXPERIMENTAL
											@doWhile v[Temp2] != 0 {

													Temp1 = v[Temp2] * 300 
													val_add(Temp1,N4700) // Temp1 += 4700
													calc_victim_distance_and_camo_to_Temp4(Temp1)

													@if Temp4 < Temp8 {
														ptr_asg(Temp6,Temp4) // set the value to v[Temp6]

														// debug 
														debug_selecting_agt(v[536],{
																func_devlog(__str("Hit. target is \v[\v[",__id(AISearch_morton_ptr),"]] "," I checked teamlist"))
														})

														// set TT2 = v[534] + Temp7
														val_asg(TT2,v[534])
														val_add(TT2,Temp7)
														@if LEGS_Common_Switch5 .isOn() {
															v[TT2] = v[Temp6] * 1000 + v[Temp2] + v[4001 + min(victim_UnitType, ClassType_Amount)]

														}.else bl{
															v[TT2] = v[Temp6] * 1000
															v[TT2].add v[Temp2],1 // v[TT1] += v[Temp2]

														}
														Temp6.add N1,2 // v[306..307] += 1
														
													}

													increment_var(Temp2) // Temp2 += 1
													
											}
											
											

										} .else bl {

											//@comment "#TT6 = morton id"
											//Isn't this formula retarded? 8.Mar
											TT5 = TT3 & 192 ? 8 : TT3 & 48 ? 6 : TT3 & 12 ? 4 : TT3 & 3 ? 2 : 0
											TT6 = TT2 >> TT5
											//@comment "#mask end"

											TT5 >>= 1 // TT5 /= 2
											defv AISearch_morton_ptr = __id(Temp12)
											AISearch_morton_ptr = v[4546 - TT5] + TT6 * v[1004] // morton agent list head
											v[327] = v[v[4552 - TT5] + TT6] // morton agent list length
											Temp33 = [1, 0, -2][Temp14]
											val_const(Temp6,3601) // Temp6 = 3601

											// deb
											debug_selecting_agt(v[536],{
													func_devlog(__str("Current selected agent is gonna search morton dim:\v[",__id(TT3), "], for \v[327] Times."))
											})

											@loop v[327] {
												// AISearch_morton_ptr = v[340] + Temp10
												// val_add(AISearch_morton_ptr,v[340])

												__if DIS_EXPERIMENTAL == 1 {
													macro_get_agent_MetaTeam_into_var(v[AISearch_morton_ptr],TT1) // AEB

												}.else bl {
													TT1 = Const_AgentMetaTeam_beginMINUSONE + v[AISearch_morton_ptr]

												}

												@if  Temp33 == TT1 { // EXPERIMENTAL - IF YOU USE AEBA  
												// @if  Temp33 == v[TT1] { // NOT EXPERIMENTAL - WITHOUT AEBA


													Temp1 = v[AISearch_morton_ptr] * v[1117] 
													val_add(Temp1,N4700) // Temp1 += 4700
													calc_victim_distance_and_camo_to_Temp4(Temp1)

													@if Temp4 < Temp8 { // in agent sight
														ptr_asg(Temp6,Temp4) // set the value to v[Temp6]
														
														// debug 
														debug_selecting_agt(v[536],{
																func_devlog(__str("Hit. found target is \v[\v[",__id(AISearch_morton_ptr),"]] "))
																func_devlog("mydim is \v[609] - current morton list address is \v[", __id(AISearch_morton_ptr),"]")
														})

														// set TT2 = v[534] + Temp7
														val_asg(TT2,v[534])
														val_add(TT2,Temp7)
														v[TT2] = v[Temp6] * 1000 + v[AISearch_morton_ptr] + v[4001 + min(victim_UnitType, 11)]

														Temp6.add N1,2 // v[306..307] += 1
														
													}
												}
												increment_var(AISearch_morton_ptr)
											}
										}

										//Has more than 16 enemies in the search radius
										@if Temp7 >= 16 {
											//set next search skip flag 
											v[agent_AI_routine_bits] |= agent_AAType == 0 ? AI_routine_bits_FLAG_AIskip : 0 //v[v[212] + 103] == 0 ? 2097152 : 0
											//break the flag
											v[agent_AI_routine_bits] &= -129 //~128
											
										} .else bl {
											//found none in the radius
											@if Temp7 == 0 {
												agent_AI_check_current_target()
													
											} .else bl {
												//break the flag
												v[agent_AI_routine_bits] &= -129//~128
													
											}
											
										}
										v[v[534]].sort Temp7
										@if `agent_AgentBits & AgentBits_FLAG_walldetected {
											agent_MapX.copy Temp11,2
											v[621].copy Temp3,2
											@if Temp4 < -30 {
												v[312] -= 1
												
											} .else bl {
												@if Temp4 > 30 {
													v[312] += 1
													
												}
												
											}

											@if Temp3 > 30 {
												Temp11 += 1
													
											} .else bl {
												@if Temp3 < -30 {
													Temp11 -= 1
													
												}
												
											}

											def MACRO_set_flag_RayCastAndPF = {
												v[v[212] + 3] |= AgentBits_FLAG_PF_walldetection_switch
												v[agent_AI_routine_bits] |= AI_routine_bits_FLAG_RayCast 

												}
											def MACRO_set_flag_walldetection = {
												v[v[212] + 3] |= AgentBits_FLAG_PF_walldetection_switch
												// v[agent_AI_routine_bits] |= AI_routine_bits_FLAG_PATHFINDING
												// v[agent_AI_routine_bits] &= AI_routine_bits_KILL_FLAG_PFcontinue //~32768

											}

											// checking tile terrain information
											v[286] = v[4529] + Temp11 - v[69] + (v[312] - v[70]) * v[2061]

											@if v[v[286]] > 0 {
												@if `v[v[286]] / 10000 % 10 == 1 { // if there's agent assigned

														Temp2 = v[v[286]] / 100000 // pick it up
														Temp1 = Temp2 * 300 
														val_add(Temp1,N4701) // Temp1 += 4701

														@if v[Temp1] == 11 {
															v[agent_AI_routine_bits] &= -193 // ~192//(64 + 128)

															@if `v[Temp1 + 99] != agent_TeamID { // not ally wall
																@if `v[agent_AI_routine_bits] & AI_routine_bits_FLAG_Pathblocked { // v[v[212] + 34] & 8388608 {
																		v[v[534]] = Temp2 // set target
																		v[v[212] + 241] = ORD_ATK
																		v[v[212] + 242] = 2

																		def AIFLAG_SkipAndPF = AI_routine_bits_FLAG_AIskip + AI_routine_bits_FLAG_PATHFINDING
																		v[agent_AI_routine_bits] |= AIFLAG_SkipAndPF // 2097152+16384
																		v[agent_AI_routine_bits] &= AI_routine_bits_KILL_FLAG_PFcontinue //~32768
																		val_add(Temp7,N1) // Temp7 += 1
																		
																} .else bl {
																		@if `v[agent_AI_routine_bits] & AI_routine_bits_FLAG_siegeweapon_flag { // it's siege unit
																				v[v[212] + 241] = ORD_ATK
																				v[v[212] + 242] = 2
																				v[v[534]] = Temp2 // set target
																				v[agent_AI_routine_bits] |= AI_routine_bits_FLAG_AIskip
																				val_add(Temp7,N1) // Temp7 += 1
																				v[v[212] + 3] |= 37748736 // 33554432 + 4194304 -> AgentBits_FLAG_PF_walldetection_switch + AgentBits_FLAG_ForcemoveFlag
																				
																		} .else bl {
																				//@comment "stuck"
																				@if agent_AAType == 0 { // melee
																						TT1 = rnd(0, 99)
																						@if TT1 <= 30 { // target stucked wall
																								v[v[534]] = Temp2
																								v[v[212] + 34] |= AI_routine_bits_FLAG_AIskip
																								val_add(Temp7,N1) // Temp7 += 1
																								
																						} .else bl { // choose raycast and seek way 
																							MACRO_set_flag_RayCastAndPF
																							
																						}
																						
																				} .else bl { // not melee
																						// MACRO_set_flag_walldetection
																						MACRO_set_flag_RayCastAndPF // start raycasting

																				}
																				
																		}
																		
																}
																
															} .else bl {
																// MACRO_set_flag_walldetection
																MACRO_set_flag_RayCastAndPF
															}
															
														}
														
												} .else bl {
													@if `!(v[agent_AI_routine_bits] & AI_routine_bits_FLAG_tracing_flag) { // just a obstacle
														MACRO_set_flag_walldetection
														
													}
													
												}
												
											} .else bl { 
												def KILL_AI_routine_bits_FLAG_tracing_flag = ~AI_routine_bits_FLAG_tracing_flag
												v[agent_AI_routine_bits] &= KILL_AI_routine_bits_FLAG_tracing_flag //-4097//~4096
											
											}
											
										}
										@if v[v[534]] < 999999999 {

											// v[v[534]..v[534] + Temp7 - 1] %= 1000
											v[buffer_for_pixel_information_head].repeat 1000,Temp7
											v[v[534]].mod v[buffer_for_pixel_information_head],Temp7

											v[Ptr20].copyFrom v[v[534]],1 // v[Ptr20] = v[v[534]]

											def KILL_AI_routine_bits_FLAG_IdleAfterEnemySearch = ~AI_routine_bits_FLAG_IdleAfterEnemySearch
											v[agent_AI_routine_bits] &= KILL_AI_routine_bits_FLAG_IdleAfterEnemySearch //-65//~64

											// debug 
											debug_selecting_agt(v[536],{
													func_devlog(__str("I finally decided to chase \v[\v[",__id(Ptr20),"]]"))
											})
								
										} .else bl {
											Temp7 = 0
											
										}

										Temp6 = 0
										TT1 = v[534] + v[1012]
										Temp6.copy v[TT1], 2

										@if v[Ptr20] == 0 {
												v[agent_AI_routine_bits] |= AI_routine_bits_FLAG_IdleAfterEnemySearch

												@if agent_StanceOrder == 3 { //`v[v[212] + 241] == 3 {
														@if agent_MovementOrder != -2 { //`v[v[212] + 242] != -2 {
																@if agent_MilPixMoveOrderedPointDist > 11000 { //`v[v[212] + 23] > 11000 {
																		v[v[212] + 245] &= ~ActionFLag_FLAG_Hold_direction
																		
																}
																
														}
														
												}

												// If agent has not been in combat for a long time
												@if agent_InCombatTimer <= -5000 { //`v[v[212] + 80] <= -5000 {
														v[agent_AI_routine_bits] |= AI_routine_bits_FLAG_AIskip // reduce enemy search frequency
														
												}
												
										}
										
								}
								
						}
						
				}
				
		}
		increment_var(v[536]) // v[536] += 1
		
}

// These processes missing comments, so idk wtf is going lmao
@if s[2] .isOff() {

		//ACTION TIMER MANAGEMENT AND SO ON
		v[536] = v[4532]
		@while v[v[536]] != 0 {

			Ptr1 = v[v[536]] * 300
			val_add(Ptr1,N4701) // Ptr1 += 4701
			v[Ptr1].copy agent_AgentType,300

			@if agent_ActionTimer <= 0 {
				val_asg(agent_ActionTimer,v[1195]) // agent_ActionTimer = v[1195] //init ActionTimer

				@if `agent_AgentBits & AgentBits_FLAG_EnableCivAI { //peasant
					agent_Morale = agent_InCombatTimer > 0 && !(agent_BaseObjBit & BaseObjBit_FLAG_Immune_to_bleed) ? 3 : 0
					//finish?

				}.else bl {
					@if `agent_AgentBits & AgentBits_FLAG_EnableBasicAI { //mil units
						@if agent_TargetAgentID == 0 { // have no target
							@if agent_MovementOrder < MovementOrder_TYPE_prioritize_moving { // and can go for his enemy agent...
								
								// then
								// try to pick from own list 
								v[211] = v[v[536]] - 1
								v[534] = v[4579] + (v[1012] + 2) * v[211]
								//pointer offset
								v[297] = v[534] + v[1012]
								//get obj amount in the list
								Temp2 = v[v[297] + 1] - v[v[297]]
								v[296] = v[534] + v[v[297]]
								v[371] = 0
								@loop Temp2 {
										TT1 = v[v[296]] * 300 
										val_add(TT1,N4701) // TT1 += 4701
										@if v[TT1] > 0  {
											agent_TargetAgentID = v[v[296]]
											val_asg(v[371],N1) // v[371] = 1
											@break
											
										}
										ptr_add(v[297],N1) // v[v[297]] += 1
										increment_var(v[296]) // v[296] += 1
										
								}
								
								agent_TargetAgentID = v[371] == 0 ? 0 : agent_TargetAgentID

							}
						}

					}

				}
				agent_AgentType.copy v[Ptr1],300
			}
			increment_var(v[536]) // v[536] += 1
						
		}


		//STATIC AI
		v[536] = v[4533]
		v[4031] = v[1146]
		v[4032..4033] = v[1145]
		v[4034] = v[206]
		v[4035..4036] = v[205]

		@while v[v[536]] != 0 {
				v[211] = v[v[536]] - 1
				v[212] = v[211] * 300 
				val_add(v[212],N5000) // v[212] += 5000
				v[v[212]].copy Address_agent_array_head,agent_Basic_Array_size

				@if agent_ActionTimer <= 0 {
						TT1 = v[212] + 84 // temp agent_ActionTimer ptr
						v[TT1] = v[1195] * 2

						@if `agent_StaticBits & 1048576 {
							Ptr20 = v[212] + 18
							Ptr19 = v[212] + 241 // ?
							Temp1 = v[212] + 100

							__if DIS_EXPERIMENTAL == 1 {
								macro_get_agent_MetaTeam_into_var(v[v[536]],Temp14) // AEB
							}.else bl {
								Temp14 = Const_AgentMetaTeam_begin + v[211]
								Temp14.deref Temp14,1
							}

							// a naru.
							Temp2 = 4031 + Temp14
							Temp2.deref Temp2,1
							Temp2.copy Temp7,1 // Temp7 = Temp2
							Temp8 = 4034 + Temp14
							// Temp8 = v[4034 + Temp14]
							Temp8.deref Temp8,1


							v[534] = v[4579] + (v[1012] + 2) * v[211]
							v[v[534]] = 0

							val_const(Temp6,3601) // Temp6 = 3601 // deploy v[3601] 'n check it
							val_const(Temp7,0) // Temp7 = 0
							@doWhile v[Temp2] != 0 {
									Temp1 = v[Temp2] * 300 
									val_add(Temp1,N4700) // Temp1 += 4700
									calc_victim_distance_and_camo_to_Temp4(Temp1)
									@if Temp4 < agent_UnitEyeSight_for_AgentSearch { // kore teki huesugitara bagurun to tyauw
											ptr_asg(Temp6,Temp4) // set the value to v[Temp6]
											TT1 = v[534] + Temp7
											v[TT1] = v[Temp6] * 1000 
											v[TT1].add v[Temp2],1
											Temp6.add N1,2
											
									}
									increment_var(Temp2) // Temp2 += 1
									
							}
							
							v[v[534]].sort Temp7
							v[Ptr20] = v[v[534]] < 999999999 ? v[v[534]] % 1000 : 0
							
						}
				}
				
				@if `agent_StaticBits & 32768 {
					@if agent_static_ReserveAmount <= 0 {
						// auto queue checker
						@call .cev 1748
						var1 = v[v[536]]
						@call .cev 1747
						var1 = v[212] + 213
						var1.deref var1,1
						@if var1 >= 1 {
								@call .cev 1730
								@call .cev 1748
								
						} .else bl {
								v[v[212] + 3] &= -32769//~32768
								
						}
						
					}
					
				}
				increment_var(v[536]) // v[536] += 1

				
		}
		
		
}



__fn agent_AI_check_current_target {
	//If the agent has target agent initially
	@if v[Ptr20] > 0 {
			//If the unit has cohort order flag

			//if the target is set by cohort order, then agent's sight radius is considered 2 times bigger than actual in this process. Besides if the agent has a target already, the sight radius gets halved..
			Temp8 <<= v[agent_AI_routine_bits] & AI_routine_bits_FLAG_AICohortGatheringAttack ? 1 : 0

			//Try checking if current target is in sight radius  
			Temp1 = v[Ptr20] * v[1117] 
			val_add(Temp1,N4700) // Temp11 += 4700
			calc_victim_distance_and_camo_to_Temp4(Temp1)

			@if Temp4 < Temp8 {
				//and if it's in the agent's sight, let it keep following to the target.
				ptr_asg(Temp6,Temp4) // set the value to v[Temp6]
				TT1 = v[534] + Temp7
				v[TT1] = v[Temp6] * 1000
				v[TT1].add v[Ptr20],1 // v[TT1] += v[Ptr20] 
				Temp6.add N1,2 // v[306..307] += 1

				// debug 
				debug_selecting_agt(v[536],{
						func_devlog(__str("Agent decided to keep chasing current target: \v[\v[",__id(Ptr20),"]] "))
				})
				
			}.else bl{
				//If the target is out of agent sight, then break cohort order flag and wait new cohort order
				v[agent_AI_routine_bits] &= -129//~128
			}
			
	}
}

__fn calc_victim_distance_and_camo_to_Temp4 $ptrToHead {
	v[$ptrToHead].copy address_victim_buffer_head, agent_Basic_Array_size
	agent_RelativeX.copy Temp4, 2
	Temp4 .sub victim_RelativeX, 2
	Temp4 .mul Temp4, 2
	val_add(Temp4,Temp5)
	val_add(Temp4,victim_ProcessCamoValue)
}

