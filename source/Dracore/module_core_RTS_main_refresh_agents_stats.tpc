__fn agent_ref_stats(){
    //@comment "remove inertia"
    agent_AgentBits &= -17//~16

    /*@comment "########################Process Parameters Setting BEGIN########################"*/
    //@comment "#Set ForceSprite Default"
		agent_ForceSprite = agent_ActionState < 4 ? -2 : agent_ForceSprite 

    // get Base Parameters to Process slots
    agent_BaseAD.copy agent_ProcessAD, 11
    agent_BaseRegHP.copy agent_ProcessHPreg, 2
    val_asg(agent_ProcessObjBit,agent_BaseObjBit) // agent_ProcessObjBit = agent_BaseObjBit
    val_asg(agent_ProcessWill,agent_BaseWill)  // agent_ProcessWill = agent_BaseWill

    // SP fatigue penalty -  exhausted units move much slower.
    agent_ProcessMS -= !(agent_ExMotionFlags & ExMotionFlags_FLAG_no_fatigue) ? muldiv(agent_ProcessMS, agent_MaxSP - agent_SP, agent_MaxSP) / 2 / (!(agent_PerkFlagBits1 & PerkFlagBits1_FLAG_Strider) ? 1 : 2) : 0
    //@comment "#ProcessCamo dealed in per3f"
    /*@comment "###############Terrain Check###############"*/

		agent_ProcessMS += agent_AgentBits & 2 ? agent_ProcessMS >> 2 : 0

    //@comment "#Water Terrain"
    @if `agent_AgentBits & 1 {
        //@comment "pathfinder"
        @if `agent_PerkFlagBits1 & agent_PerkFlagBits1_FLAG_Pathfinder {
            agent_ProcessMS -= agent_ProcessMS >> 2 // muldiv(agent_ProcessMS, 3, 4)
            val_sub(agent_ProcessSPReg,N1) // agent_ProcessSPReg -= 1
            
        } .else bl {
						
            // agent_ProcessEVA -= agent_ProcessEVA >> 2 
						// this might cause bug when EVA is below zero
						
            agent_ProcessEVA -= agent_ProcessEVA > 0 ? agent_ProcessEVA >> 2 : 5 // if below 0 already, -5
            agent_ProcessMS >>= 1 // /= 2
            val_sub(agent_ProcessSPReg,N2)// agent_ProcessSPReg -= 2
            
        }
        
    }
    //@comment "#Ice"
    @if `agent_AgentBits & 32 {
        agent_AgentBits |= 16
        agent_ProcessMS += agent_ProcessMS >> 2 // muldiv(agent_ProcessMS, 125, 100)
        
    }

    // cavalries and terrain effects
		@if `!(agent_MoveTypeBIts & MoveTypeBits_Flying){ // not flying
			@if `agent_ExMotionFlags & agent_ExMotionFlags_FLAG_horseback {
				@if `agent_PerkFlagBits1 & PerkFlagBits1_FLAG_Pathfinder { // Pathfinder
					agent_ProcessMS = agent_AgentBits & AgentBits_horse_in_forest ? muldiv(agent_ProcessMS, 80, 100) : agent_ProcessMS // cav in forest
						@if `agent_ProcessInstantState & agent_ProcessInstantState_FLAG_climb_hill {
							agent_ProcessMS >>= 1 // /= 2 //cav on hill 
								agent_SP -= 8
								agent_SP = max(agent_SP,0)
						}
				}.else bl{
					agent_ProcessMS = agent_AgentBits & AgentBits_horse_in_forest ? muldiv(agent_ProcessMS, 60, 100) : agent_ProcessMS //cav in forest
						@if `agent_ProcessInstantState & agent_ProcessInstantState_FLAG_climb_hill {
							agent_ProcessMS /= 3
								agent_SP -= 12
								agent_SP = max(agent_SP, 0)
						}
				}
			}.else bl{ //infantries

				@if `agent_PerkFlagBits1 & agent_ExMotionFlags_FLAG_horseback {//Pathfinder
					agent_ProcessMS -= agent_ProcessInstantState & agent_ProcessInstantState_FLAG_climb_hill ? agent_ProcessMS >> 5 : 0 
				}.else bl{
					@if `agent_ProcessInstantState & agent_ProcessInstantState_FLAG_climb_hill {
						agent_ProcessMS -= agent_ProcessInstantState & agent_ProcessInstantState_FLAG_climb_hill ? agent_ProcessMS >> 4 : 0
							agent_SP -= 5
							agent_SP = max(agent_SP,0)
					}
				}
			}
		}

    /*@comment "###################Terrain Check End###################"*/
    //after check, ProcInstantState reset
    agent_ProcessInstantState &= 48 // agent_ProcessInstantState_FLAG_shield_covered + agent_ProcessInstantState_FLAG_shield_covered_double_check //= 0 Instant States fade per refresh except few

    // Check TileFlags begin

    Ptr5 = Const_save_var_TileFlags + agent_TileLink

	
	__if DEBUG_BUILD == 1 {
		@if v[Ptr5] > 3 {
			var1 = v[Ptr5] 
			func_devlog("TileFlag found! its value is \v[11]")
		}
	}

		//check aura effect only per 20f
		// TT1 = AllFrameCount % 5 -> module_core_RTS_main.tpc, v[TPC_VAR_STACK_HEAD]
		@if v[TPC_VAR_STACK_HEAD] == 0 {

			__if DIS_EXPERIMENTAL == 1 {
				// get Meta team into TT10
				macro_get_agent_MetaTeam_into_var(v[v[536]], TT10) // AEB
			}.else bl {
				TT10 = Const_AgentMetaTeam_begin + v[v[536]]

				val_sub(TT10,N1) // TT10 -= 1
				TT10.deref TT10,1 // TT10 = v[TT10]
			}

			// SHIELD WALL COVER
			@if `v[Ptr5] & (FLAG_Maptile_ShieldCoverTeam0 << TT10) { // covered by other guy's shield wall
				// EXPERIMENTAL - just check if it's working
				agent_ProcessInstantState |= agent_ProcessInstantState & agent_ProcessInstantState_FLAG_shield_covered ? agent_ProcessInstantState_FLAG_shield_covered_double_check : agent_ProcessInstantState_FLAG_shield_covered //needs double check
				// andthen

			}.else bl{
				agent_ProcessInstantState &= -49 //~(agent_ProcessInstantState_FLAG_shield_covered + agent_ProcessInstantState_FLAG_shield_covered_double_check)
			}
			// Big units like cavalries cannot be covered by infantry shieldwall
			agent_ProcessInstantState &= agent_BaseObjBit & BaseObjBit_FLAG_big_unit ? -49 : agent_ProcessInstantState

			// FLAG_Maptile_PikeWallTeam0 = 256
			// FLAG_Maptile_PikeWallTeam1 = 512
			// FLAG_Maptile_PikeWallTeam2 = 1024
			// check if under other team's pike effect or not
			@if agent_UnitType == UNIT_CLASS_CAV {
				agent_MeleeFightTimer = v[Ptr5] & (1792 & ~(FLAG_Maptile_PikeWallTeam0 << TT10))&& !(agent_ProcessObjBit & 128) ? Const_Melee_Fight_Time_by_Melee_Attack : agent_MeleeFightTimer // Cavalries near enemy pike wall will automatically get Melee fighting Penalty
			}
			
				
				}

		/* this was OP
		@if `agent_ProcessInstantState & agent_ProcessInstantState_FLAG_shield_covered_double_check {
			agent_ProcessEVA += 5 
		}
		*/



    //Check TileFlags end


    //@comment "#Buff/Debuff Effects"
    s[119].on
		def AGT_STATS_RESET_BUFF_PTRS = {
			// v[234] = 671, v[235] = 686

			v[234].repeat __id(agent_Buff1),2
			val_add(v[234],N15)
		}

		AGT_STATS_RESET_BUFF_PTRS

    v[ptr_null].copy v[371],1 // init flag
    @while v[v[235]] > v[1186] {
        @call .cev v[v[235]]
        //@comment "#タイマー切れてたら消す"
        @if v[v[234]] <= 0 {
						ptr_init(v[235]) // v[v[235]] = 0
            val_or(v[371],N1) // buff timer finished
            
        }
        // increment_var(v[235]) // v[235] += 1
				v[234].add N1,2
        
    }

		// buff removed
    @if v[371] == 1 {
				AGT_STATS_RESET_BUFF_PTRS
        v[v[235]].sortDescending 8 .sync v[v[234]]
    }

    s[119].off
    /*@comment "###############Perk Check###############"*/

    // Last Stand Perk effect: The agent gains Attack Speed bonus based on missing Health ratio.
    agent_ProcessAS += agent_PerkFlagBits1 & 524288 ? divmul(agent_ProcessAS, agent_MaxHP, agent_MaxHP - agent_HP) : 0
		
    // @comment "###################Perk Check End###################"



		// ##############Morale Check###############"
		
    TT2 = 10 - agent_Morale // save (10 - agent_Morale)
    v[862..866] *= TT2 // no need to replace with 2 line array cont, since this is faster in this case
		agent_ProcessAD.div N10,4 // v[862..866] /= 10

    //@comment "##Flee"
    @if agent_Morale == 3 {
        agent_ProcessInstantState |= agent_ProcessInstantState_FLAG_blind
        agent_ProcessObjBit |= 67110400 // 1024+512+67108864
        agent_ProcessObjBit &= -16793601 // ~16793600//(16777216+16384)
        agent_MovementOrder = 11
        agent_ActionState = 0
        //@comment "#Break Discipline"
        //@comment "Will/4"
        //agent_ProcessWill /= 4
				agent_ProcessWill >>= 2
        //agent_ProcessRange_max = v[869..870] = [9899999, 0]
				agent_ProcessRange_min = 9899999 //is it fucking serious? what is going on?
				agent_ProcessRange_max = 0
        agent_ProcessMS += agent_ProcessMS >> 3 //muldiv(agent_ProcessMS, 110, 100)
        
    } .else bl {
        agent_ProcessWill = muldiv(agent_ProcessWill, TT2, 10)
        //@comment "#Mages"
        @if agent_UnitType == 4 {

            // @comment "Player only"
            @if agent_TeamID == 0 {
                agent_ProcessAP += muldiv(agent_BaseAP, 4 * v[493], 100) + v[493] // university bonus
                
            }
            
        } .else bl {
            //@comment "#Civilians - worker units"
            @if agent_UnitType == 9 {
								val_asg(agent_ProcessAD,agent_BaseAD) // agent_ProcessAD = agent_BaseAD

								//get tech slot 2!
                // v[341] = v[2409 + agent_TeamID % 3]
								TT1 = agent_TeamID % 3 // kari.. you can use META TEAM
								func_get_ptr_to_player_tech_slot_into_r1(TT1,2)
								// now reg1 is ptr to tech slot 2
									
                //@comment "cart1"
                @if `v[reg1] & 16 {
                    agent_ProcessAS +=  agent_BaseAS >> 3 // muldiv(agent_BaseAS, 12, 100): actually give +12.5% for performance
                    agent_ProcessMS += agent_ProcessMS >> 3 // muldiv(agent_ProcessMS, 11, 10)
                    
                }
                //@comment "cart2"
                @if `v[reg1] & 32 {
                    agent_ProcessAS += agent_BaseAS >> 3 // muldiv(agent_BaseAS, 10, 100) - actually give +12.5% for performance
                    agent_ProcessMS += agent_ProcessMS >> 3 // muldiv(agent_ProcessMS, 11, 10)
                    // agent_ProcessMS = muldiv(agent_ProcessMS, 11, 10)
                    
                }
                //@comment "crane"
                agent_ProcessAD += v[reg1] & 64 ? agent_ProcessAD >> 2 : 0 // muldiv(agent_ProcessAD, 20, 100)
                
            }
            
        }



        /*"###############Fighting Order###############"*/
        //v[341] = agent_TacticsOrderFlag
        @if agent_TacticsOrderFlag >= 1 {
															
								@if `agent_TacticsOrderFlag & 2 {
									@if agent_SP > 10 {
										@if `!(agent_ProcessObjBit & 67108864) {
											//1.62BETA2... nerfed, because we introduce the team shieldwall aura effect
											agent_ProcessAR += agent_BaseAR >> 2 // muldiv(agent_BaseAR, 30, 100) 30%->25%
												agent_ProcessMR += agent_BaseMR >> 2 // muldiv(agent_BaseMR, 30, 100) 30%->25
												agent_ProcessEVA += agent_BaseEVA / 3  // muldiv(agent_BaseEVA, 40, 100) 40%->33%
												@if `agent_ProcessInstantState & agent_ProcessInstantState_FLAG_shield_covered_double_check {
													agent_ProcessEVA += agent_ProcessEVA >> 3
														agent_ProcessAR += agent_BaseAR >> 4 // +6%
														agent_ProcessMR += agent_BaseMR >> 4 // +6%
														agent_ProcessEVA += 3
												}

											//single shield wall give you 33% bonus so you have 133% base 
											//And team shield wall will additinally give unit ([Current EVA]*8%) 
											//so bacially mass weaker shield wall gives you 141% boost at least  - Eva effect is bit stronger than before1.162.

												agent_ProcessAS >>= 2 // /= 4
												agent_ProcessAS *= agent_AABits & AABits_FLAG_AntiCav ? 3 : 2 // if agent's weapon is spear -25%. otherwise -50%
												val_sub(agent_ProcessSPReg,N1)

												agent_ProcessMS = muldiv(agent_ProcessMS, 8, 10)
												//@comment "#Force Spr"
												val_asg(agent_ForceSprite,N2) // agent_ForceSprite = 2
										}
									}
								}.elif `agent_TacticsOrderFlag & 4 { //stronger shield wall
									@if agent_SP > 10 {
										@if `!(agent_ProcessObjBit & 67108864) {
											agent_ProcessAR += agent_BaseAR / 3 //muldiv(agent_BaseAR, 40, 100) 40%->33%
												agent_ProcessMR += agent_BaseMR / 3 //muldiv(agent_BaseMR, 40, 100) 40%->33%
												agent_ProcessEVA += muldiv(agent_BaseEVA, 40, 100)//50%->40%
												@if `agent_ProcessInstantState & agent_ProcessInstantState_FLAG_shield_covered_double_check {
													agent_ProcessEVA += agent_ProcessEVA >> 3//
														agent_ProcessAR += agent_BaseAR >> 4 //+6%
														agent_ProcessMR += agent_BaseMR >> 4 //+6%
														agent_ProcessEVA += 3
												}

											//single strong shield wall gives you 40% bonus so you have 140% base
											//then team shield wall gives you ([Current EVA]*12%)
											//so basically mass shield wall gives you 148% base - it seems bit weaker than before 1.162 - but still you have +3 assured eva bonus + anti ranged bonus.

												agent_ProcessAS >>= 2 // /= 4
												agent_ProcessAS *= agent_AABits & AABits_FLAG_AntiCav ? 3 : 2 // if agent's weapon is spear -25%. otherwise -50%
												val_sub(agent_ProcessSPReg,N1)

												agent_ProcessMS = muldiv(agent_ProcessMS, 8, 10)
												agent_ProcessObjBit |= 8192
												//@comment "#Force Spr"
												val_asg(agent_ForceSprite,N2)
												// agent_ForceSprite = 2
										}
									}
																		
                }.elif `agent_TacticsOrderFlag & 1 { // pikewall
                    agent_ProcessHIT = muldiv(agent_ProcessHIT, 12, 10)
                    agent_ProcessAS -= agent_ProcessAS >> 3

                    agent_ProcessRange_max += 5000
                    agent_ProcessMS /= 2
                    //@comment "##knock back immunity"
                    agent_ProcessObjBit |= BaseObjBit_FLAG_Immune_to_Knock_back
                    //@comment "#ForceSpr"
                    agent_ForceSprite = 2
                    
                } .elif `agent_TacticsOrderFlag & 8 { //@comment "##Skirmishing" 
                    agent_ProcessRange_min = agent_ProcessRange_max / 2
                    agent_ProcessRange_max = muldiv(agent_ProcessRange_max, 9, 10)
                    agent_ProcessHIT = muldiv(agent_ProcessHIT, 9, 10)
                    agent_ProcessAS = muldiv(agent_ProcessAS, 14, 10)
                    
                } .elif `agent_TacticsOrderFlag & 16 {//@comment "##Cast Support"
                    agent_ProcessRange_min = agent_ProcessRange_max / 2
                    agent_ProcessRange_max += agent_ProcessRange_max >> 3 //muldiv(agent_ProcessRange_max, 11, 10)
                    agent_ProcessMS += agent_ProcessMS >> 2 //muldiv(agent_ProcessMS, 75, 100)
                    agent_ProcessSPReg += 2
                    
                }.elif `agent_TacticsOrderFlag & 64 {//@comment "##Rallying"
                    agent_ProcessAR += muldiv(agent_BaseAR, 10, 100)
                    agent_ProcessMR += muldiv(agent_BaseMR, 10, 100)
                    agent_ProcessAS /= 2
                    agent_ProcessMS = muldiv(agent_ProcessMS, 8, 10)
                    agent_ProcessWill += agent_ProcessWill / 2 //muldiv(agent_ProcessWill, 50, 100)
                    agent_ProcessCamoValue = 0
                    
                }.elif `agent_TacticsOrderFlag & 128 {//@comment "##Observation Flag"
                    agent_ProcessAR += muldiv(agent_BaseAR, 10, 100)
                    agent_ProcessMR += muldiv(agent_BaseMR, 10, 100)
                    agent_ProcessAS /= 2
                    agent_ProcessMS = muldiv(agent_ProcessMS, 8, 10)
                    agent_ProcessWill += muldiv(agent_ProcessWill, 50, 100)
                    agent_ProcessCamoValue += 500000
                    
                }

                
            
            
        }
        /*#########################Fighting Order END########################"*/
        
    }
    /*@comment "##################Morale Check End##################"*/
		//@comment "#In Melee Combat"
		@if agent_MeleeFightTimer > 1 {
			agent_ProcessMS -= agent_ProcessMS >> 2 //= divmul(agent_ProcessMS, 100, 75)
				agent_ProcessAS -= agent_ProcessAS >> 3
				agent_ProcessSPReg -= agent_ProcessSPReg >> 2 //= muldiv(agent_ProcessSPReg,5,4) //-25%
				agent_ProcessObjBit &= -134234114 //~134234113(BaseObjBit_FLAG_SpeedBonus) //~16384 & ~1 & ~134217728

				//Crowded?
				agent_ProcessMS -= v[Ptr5] & FLAG_Maptile_Crowded ? agent_ProcessMS >> 3 : 0 //-12.5%

		}.elif `agent_ProcessObjBit & BaseObjBit_FLAG_SpeedBonus {

			agent_ProcessAD += agent_AAType != 1 ? agent_ProcessMS / 10 : 0 //if not ranged troop, gain 10% of MS now 8%->10%

		}

		/*@comment "########################
			Process Parameters Setting END
########################"*/
}



